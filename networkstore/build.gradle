plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'com.google.protobuf'
}

apply from: "${rootProject.projectDir}/config/gradle/commonLibraryConfig.gradle"

android {
    compileSdk project.compileSdkVersion

    defaultConfig {
        minSdk project.minSdkVersionApp
        targetSdk project.targetSdkVersion
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    sourceSets{
        main.java.srcDirs += "${protobuf.generatedFilesBaseDir}/main/javalite"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {

//    implementation project(path: ':common')

    implementation appDependencies.kotlin
    implementation appDependencies.corektx
    implementation appDependencies.jodatime

    implementation diDependencies.dagger
    implementation diDependencies.daggerAndroid
    kapt diDependencies.daggerCompiler
    kapt diDependencies.daggerCompilerAndroid

//todo poprawic
    implementation 'com.google.protobuf:protobuf-lite:3.0.1'
    implementation 'io.grpc:grpc-okhttp:1.43.2'
    implementation 'io.grpc:grpc-protobuf-lite:1.43.2'
    implementation 'io.grpc:grpc-stub:1.43.2'

}

protobuf {
    protoc {
        // You still need protoc like in the non-Android case
        artifact = 'com.google.protobuf:protoc:3.0.0'
    }
    plugins {
        javalite {
            // The codegen for lite comes as a separate artifact
            artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                // In most cases you don't need the full Java output
                // if you use the lite output.
                remove java
            }
            task.plugins {
                javalite { }
            }
        }
    }
}